set shell := ["bash", "-eEuo", "pipefail", "-c"]
set dotenv-load := true
# Variables from .env override environment variables inherited by `just` from parent.
set dotenv-override := true

export AWS_PAGER := ""
# Must match opentofu config!
export AWS_REGION := env("AWS_REGION", "eu-central-1")

aws := require("aws")
jq := require("jq")
tofu := require("tofu")

default:
    @{{ just_executable() }} --list

[confirm("Do you really want to setup remote state?")]
[group("tofu")]
[working-directory("remote_state_setup")]
init_remote_state environment_name subproject_name:
    tofu init
    tofu apply -var="env={{ environment_name }}" -var="subproject={{ subproject_name }}"
    aws s3 cp terraform.tfstate "s3://$(tofu output -json | jq -er ".backend_config.value.s3.bucket")/remote_state_setup.tfstate"
    @echo "Remember to set backend options in appropriate "terraform.tf" using the configuration below."
    tofu output
    @rm terraform.tfstate terraform.tfstate.backup || true

[group("security")]
[group("tofu")]
tfsec:
    tfsec --config-file tfsec.yml .

[group("tofu")]
fmt:
    tofu fmt -recursive --check .

[group("tofu")]
fmt_fix:
    tofu fmt -recursive .

[group("tofu")]
policies:
    echo "Placeholder for policies command with opa and conftest"

[group("tofu")]
tf env subproject +ARGS:
    tofu -chdir="env/{{ env }}/{{ subproject }}" {{ ARGS }}

[group("docker")]
log_into_ecr env:
    aws ecr get-login-password --region "{{ AWS_REGION }}" \
        | docker login --username AWS --password-stdin "$({{ just_executable() }} tf {{ env }} backend output -json | jq -er .ecr_prefix.value )"

[group("docker")]
[group("tofu")]
publish_yagna env yagna_tag: (log_into_ecr env)
    docker build "docker/yagna" --build-arg="YAGNA_TAG={{ yagna_tag }}" -t "$({{ just_executable() }} tf {{ env }} backend output -json | jq -er .ecr_url.value.yagna ):{{ yagna_tag }}" --push

[group("docker")]
[group("tofu")]
publish_backend env tag: (log_into_ecr env)
    docker build "../webapp/backend" -t "$({{ just_executable() }} tf {{ env }} backend output -json | jq -er .ecr_url.value.backend ):{{ tag }}" --push

[group("secrets")]
read_ssm_secret env module secret_tf_output_path:
    aws ssm get-parameter \
        --with-decryption \
        --name "$({{ just_executable() }} tf {{ env }} {{ module }} output -json | jq -er .ssm_secret_parameter_names.value.{{ secret_tf_output_path }} )"

[group("secrets")]
set_ssm_secret env module secret_tf_output_path secret_value:
    aws ssm put-parameter \
        --name "$({{ just_executable() }} tf {{ env }} {{ module }} output -json | jq -er .ssm_secret_parameter_names.value.{{ secret_tf_output_path }} )" \
        --type SecureString \
        --tier Standard \
        --data-type text \
        --overwrite \
        --value "{{ secret_value }}"

[group("maintenance")]
pre_destroy_cleanup env subproject:
    @echo "Running pre-destroy cleanup for {{ env }}/{{ subproject }}"
    bash scripts/pre-destroy-cleanup.sh {{ env }} {{ subproject }}

[group("maintenance")]
check_ecs_health env:
    @echo "Checking ECS cluster health for {{ env }}"
    aws ecs describe-clusters --clusters $(just tf {{ env }} backend output -json | jq -r .ecs_cluster_name.value)
    aws ecs list-container-instances --cluster $(just tf {{ env }} backend output -json | jq -r .ecs_cluster_name.value)
    aws ecs describe-container-instances --cluster $(just tf {{ env }} backend output -json | jq -r .ecs_cluster_name.value) --container-instances $(aws ecs list-container-instances --cluster $(just tf {{ env }} backend output -json | jq -r .ecs_cluster_name.value) --query 'containerInstanceArns[]' --output text) --query 'containerInstances[].{Status:status,AgentConnected:agentConnected,RunningTasks:runningTasksCount}'

[group("tofu")]
safe_destroy env subproject: (check_ecs_health env) (pre_destroy_cleanup env subproject)
    tofu -chdir="env/{{ env }}/{{ subproject }}" destroy


# TODO: copy website deployment recipes from golembase
